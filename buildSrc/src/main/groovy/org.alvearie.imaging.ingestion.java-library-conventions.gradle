/*
 * (C) Copyright IBM Corp. 2021
 *
 * SPDX-License-Identifier: Apache-2.0
 */
plugins {
    id 'org.alvearie.imaging.ingestion.common-conventions'

    // Apply the common convention plugin for shared build configuration between library and application projects.
    id 'org.alvearie.imaging.ingestion.java-common-conventions'

    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'

    // Apply maven-publish plugin for publishing artifacts
    id 'maven-publish'
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

ext {
    jarPublishVersion = project.version
    if (!project.hasProperty('isReleaseBuild') || isReleaseBuild != "true") {
        jarPublishVersion = project.version + "-SNAPSHOT"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = jarPublishVersion
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Alvearie/imaging-ingestion"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
